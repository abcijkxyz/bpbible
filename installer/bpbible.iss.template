; Directions for use
; ==================
; (NB: These instructions are encapsulated by the make_release.py script).
; 1. Download BPBible from SVN (or whereever). Run "python setup.py py2exe".
;    This will compile BPBible for Windows.
; 2. Copy gdiplus.dll from the wxWidgets folder (e.g. wx-2.8-msw-unicode\wx\
;    gdiplus.dll) to this folder (installer\gdiplus.dll)
; 3. You may need to update any paths that have changed for the
;    [UninstallFiles] section - an official change may be being made for
;    the location of *.idx files and per-user setting may become (semi)
;    standard. (%APPDATA% is {userappdata})
; 4. Run InnoSetup and compile this file!

[Setup]
AppName=BPBible
AppVerName=BPBible $APP_VERSION_LONG_NAME
AppPublisher=BPBible Development Team
AppPublisherURL=http://BPBible.com/
AppSupportURL=http://BPBible.com/
AppUpdatesURL=http://BPBible.com/
AppReadmeFile={app}\README.txt
AppCopyright=© 2007 - 2011 BPBible Development Team
AppId=BPBible
AppVersion=$APP_VERSION_LONG_NAME
VersionInfoCompany=BPBible
VersionInfoCopyright=© 2007 - 2011 BBPBible Development Team
VersionInfoDescription=BPBible is a flexible Bible Study tool...
VersionInfoTextVersion=$APP_VERSION_LONG_NAME
VersionInfoVersion=$APP_VERSION_RAW_NUMBER.0.0.0.0
DefaultDirName={pf}\BPBible
DefaultGroupName=BPBible
AllowNoIcons=yes
InfoBeforeFile=Info.rtf
OutputDir=.
OutputBaseFilename=bpbible-$APP_VERSION_SHORT_NAME-setup
SetupIconFile=..\graphics\bpbible.ico
SolidCompression=yes
Compression=lzma/ultra
InternalCompressLevel=ultra
WizardImageFile=installer-side.bmp
WizardSmallImageFile=installer-small.bmp

Uninstallable=yes
UninstallDisplayIcon={app}\bpbible.exe

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "..\dist\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "uninstall.ico"; DestDir: "{app}"; Flags: ignoreversion
; Shared library needed for Windows 2000 and down. Redistributable, so it's legal.
Source: "gdiplus.dll"; DestDir: "{app}"; OnlyBelowVersion: 5.0,5.01


[Dirs]
;Name: "{app}\resources"
Name: "{app}\data"

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
var SWORDPath: String;
begin
	if CurStep=ssPostInstall then begin
		// Move the path configuration file into the user directory if it has been installed in the app directory (BPBible 0.4.x and down).
		if FileExists(ExpandConstant('{app}\sword.conf')) then begin
			FileCopy(ExpandConstant('{app}\sword.conf'), ExpandConstant('{userappdata}\bpbible\sword.conf'), True);
		end;

		// Detect The SWORD Project for Windows and set the resources path to include it.
		if (not FileExists(ExpandConstant('{userappdata}\bpbible\sword.conf'))) and
			RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\sword.exe', 'Path', SWORDPath) then
		begin
			SetIniString('Install', 'DataPath', SWORDPath, ExpandConstant('{userappdata}\bpbible\sword.conf'));
		end;
	end;
end;
[EndCode]

[Icons]
Name: "{group}\BPBible"; Filename: "{app}\bpbible.exe"; Comment: "Run BPBible - read the Bible!"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallProgram,BPBible}"; Filename: "{uninstallexe}"; IconFilename: "{app}\uninstall.ico"; Comment: "Uninstall BPBible"
Name: "{userdesktop}\BPBible"; Filename: "{app}\bpbible.exe"; Tasks: desktopicon; Comment: "Run BPBible - read the Bible!"; WorkingDir: "{app}"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\BPBible"; Filename: "{app}\bpbible.exe"; Tasks: quicklaunchicon; Comment: "Run BPBible - read the Bible!"; WorkingDir: "{app}"

[UninstallDelete]
;Error log
Type: files; Name: "{app}\bpbible.exe.log"
;Paths file
Type: files; Name: "{app}\paths.ini"
;SWORD modules path configuration file
Type: files; Name: "{app}\sword.conf"
Type: files; Name: "{userappdata}\bpbible\sword.conf"
;BPBible data
Type: files; Name: "{app}\data\data.conf"
Type: files; Name: "{userappdata}\bpbible\data.conf"
;Search indexes
Type: files; Name: "{app}\*.idx"
Type: files; Name: "{userappdata}\bpbible\indexes\*.idx"
